# Тезисы

* Торговая компания покупает и продает товары/услуги. Товары и услуги указываются в одной табличной части
* Учет товаров по складам не ведется
* Контроль остатков с оповещением о нехватке товаров при продаже
* Списание себестоимости товаров должно быть организовано по партиям и зависеть от текущей учетной политики
* Учетная политика устанавливается год. На следующий год может измениться
* Структуры требуемых отчетов
  * Продажи: Номенклатура, Количество, Себестоимость, Стоимость, Прибыль, где Прибыль считается как "Стоимость - Себестоимость"
  
        |   Номенклатура    |   Количество  |   Себестоимость   |   Стоимость   |   Прибыль |
        -------------------------------------------------------------------------------------
        |   Торт Тирамису   |   2           |   1400            |   2600        |   1200    |
        |   Кекс            |   6           |   240             |   600         |   360     |
        |   Доставка        |   1           |                   |   100         |   100     |

  * Остатки: Номенклатура, Партия, Количество, Стоимость с группировкой по полю Номенклатура
  
        |   Номенклатура    |   Партия                  |   Количество  |   Стоимость   |
        ---------------------------------------------------------------------------------
        |   Торт Тирамису   |                           |   3           |   2100        |
        |                   |   Приходная накладная 1   |   2           |   1400        |
        |                   |   Приходная накладная 2   |   1           |   700         |
        |   Кекс            |                           |   10          |   400         |
        |                   |   Приходная накладная 1   |   7           |   280         |
        |                   |   Приходная накладная 3   |   3           |   120         |

## Решение

### Исследование тезисов

* ***"Торговая компания покупает и продает товары/услуги. Товары и услуги указываются в одной табличной части".*** Для операций покупки/продажи потребуются 2 документа - "Приходная накладная" и "Расходная накладная". Среди товаров будут услуги, значит, требуется справочник "Товары" с признаком, услуга это или нет.
* ***"Учет товаров по складам не ведется".*** Значит, весть товар организации хранится на одном складе и хранить остатки в разрезе складов не требуется.
* ***"Контроль остатков с оповещением о нехватке товаров при продаже".*** При проведении документа "Расходная накладная" необходимо учитывать остатки товаров и, если товара не хватает, запрещать проводить документ, сообщать о нехватке товара.
* ***"Списание себестоимости товаров должно быть организовано по партиям и зависеть от текущей учетной политики. Учетная политика устанавливается год. На следующий год может измениться".*** Нужно вести учет в разрезе партий. Т.к. значения учетной политики изменяться не будут, для их хранения используем объект Перечисление. Списание стоимости нужно организовать по методу, установленному учетной политикой на текущий год. Для учетной политики будем использовать регистр сведений с периодичностью "Год".

Исходя из структуры требуемых отчетов видно, что нужно знать остатки товаров и продажи. В обоих случаях будем использовать регистры накопления. Для учета остатков - регистр накопления Остатки товаров с полями Номенклатура, Партия, Количество, Стоимость, где партия - документ "Приходная накладная". Для учета продаж буем использовать оборотный регистр накопления с полями Номенклатура, Количество, Стоимость, Себестоимость. Прибыль записывать не будем, так как ее можно рассчитать как "Стоимость - Себестоимость".

### Разработка

#### Начнем разработку с изменения структуры данных

Структура измененных объектов:

* Справочники
  * Номенклатура
    * Реквизиты
      * ВидНоменклатуры (ПеречислениеСсылка.ВидыНоменклатуры)
* Регистры сведений
  * УчетнаяПолитика
    * Периодичность - В пределах года
    * Ресурсы
      * УчетнаяПолитика (ПеречислениеСсылка.УчетнаяПолитика)
* Регистры накопления
  * ОстаткиНоменклатуры
    * Вид регистра - Остатки
    * Измерения
      * Номенклатура (СправочникСсылка.Номенклатура)
      * Партия (ДокументСсылка.ПриходнаяНакладная)
    * Ресурсы
      * Количество (Число, 10.0)
      * Стоимость (Число, 12.2)
  * Продажи
    * Вид регистра - Обороты
    * Измерения
      * Номенклатура (СправочникСсылка.Номенклатура)
    * Ресурсы
      * Количество (Число, 10.0)
      * Стоимость (Число, 12.2)
      * Себестоимость (Число, 12.2)

### Проведение документа Приходная накладная

* Способ 1. Для создания движений по документу воспользуемся Конструктором движений. Он формирует такой код:

    ```1C
    Процедура ОбработкаПроведения(Отказ, Режим)
      // регистр ОстаткиНоменклатуры Приход
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
        Движение.Партия = Ссылка;
        Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
        Движение.Стоимость = ТекСтрокаСписокНоменклатуры.Сумма;
      КонецЦикла;
    КонецПроцедуры
    ```
  Плюсы заполнения движений конструктором:
  * Документ уже в памяти, поэтому его не нужно читать из базы
  
  Минусы:
  * Для того, чтобы избежать попадания на остатки услуг, нужно изменить код. Для получении значения поля ВидНоменклатуры элемента справочника Номенклатура система выполнит неявный запрос к БД, что повлияет на производительность во время проведения документа

    ```1C
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      // регистр ОстаткиТоваров Приход
      Движения.ОстаткиТоваров.Записывать = Истина;
      Для Каждого ТекСтрокаТовары Из Товары Цикл
        Если ТекСтрокаСписокНоменклатуры.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
          Продолжить;
        КонецЕсли;
        Движение = Движения.ОстаткиТоваров.Добавить();
        Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
        Движение.Период = Дата;
        Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
        Движение.Партия = Ссылка;
        Движение.Количество = ТекСтрокаТовары.Количество;
        Движение.Стоимость = ТекСтрокаТовары.Стоимость;
        КонецЕсли;
      КонецЦикла;
    КонецПроцедуры
    ```
  * Не производится группировка по номенклатуре, из-за чего в движения по ней заполняются несколько раз

* Способ 2. Перед заполнением движений выполним запрос к БД, в котором выберем все записи табличной части этого документа без услуг и сгруппируем их по номенклатуре:

    ```1C
    Процедура ОбработкаПроведения(Отказ, Режим)
      Запрос = Новый Запрос;
      Запрос.Текст =
        "ВЫБРАТЬ
        |  ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
        |  &Период,
        |  ПриходнаяНакладнаяСписокНоменклатуры.Ссылка КАК Партия,
        |  ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |  СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
        |  СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость
        |ИЗ
        |  Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
        |ГДЕ
        |  ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
        |  И НЕ ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
        |
        |СГРУППИРОВАТЬ ПО
        |  ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
        |  ПриходнаяНакладнаяСписокНоменклатуры.Ссылка";

      Запрос.УстановитьПараметр("Период", Дата);
      Запрос.УстановитьПараметр("Ссылка", Ссылка);

      Выборка = Запрос.Выполнить().Выбрать();

      Пока Выборка.Следующий() Цикл
        Движение = Движения.ОстаткиНоменклатуры.Добавить();
        Движение.ВидДвижения = Выборка.ВидДвижения;
        Движение.Период = Выборка.Период;
        Движение.Номенклатура = Выборка.Номенклатура;
        Движение.Партия = Выборка.Партия;
        Движение.Количество = Выборка.Количество;
        Движение.Стоимость = Выборка.Стоимость;
      КонецЦикла;

      Движения.ОстаткиНоменклатуры.Записывать = Истина;
    КонецПроцедуры
    ```

Все поля, которые будут подставляться в записи движений, уже выбраны в запросе и в дальнейшем получаются из выборки. Не забываем в конце (или начале) указать, что при проведении должны записаться данные по регистру Остатки номенклатуры. Остановимся на втором решении, т.к. записи сгруппированы и нет не явных обращений к БД.

### Проведение документа Расходная накладная

Для формирования движений по документу Расходная накладная сначала формируем движения только по регистру Остатки товаров. Текст получившегося модуля:

    ```1C
    Процедура ОбработкаПроведения(Отказ, РежимПроведения)
      // Считываем учетную политику
      УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
      Если ЗначениеЗаполнено(УчетнаяПолитика.УчетнаяПолитика) Тогда
        МетодСписания = УчетнаяПолитика.УчетнаяПолитика;

        // Делаем выборку данных
        Запрос = Новый Запрос;
        Запрос.Текст =
          "ВЫБРАТЬ
          |  МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
          |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
          |  СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
          |  СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
          |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
          |ПОМЕСТИТЬ втДанныеДокумента
          |ИЗ
          |  Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
          |ГДЕ
          |  РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
          |
          |СГРУППИРОВАТЬ ПО
          |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
          |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
          |
          |ИНДЕКСИРОВАТЬ ПО
          |  Номенклатура
          |;
          |
          |////////////////////////////////////////////////////////////////////////////////
          |ВЫБРАТЬ
          |  втДанныеДокумента.НомерСтроки КАК НомерСтроки,
          |  втДанныеДокумента.Номенклатура КАК Номенклатура,
          |  втДанныеДокумента.Количество КАК Количество,
          |  втДанныеДокумента.Стоимость КАК Стоимость,
          |  втДанныеДокумента.ВидНоменклатуры КАК ВидНоменклатуры,
          |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
          |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
          |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.Партия, ЗНАЧЕНИЕ(Документ.ПриходнаяНакладная.)) КАК Партия
          |ИЗ
          |  втДанныеДокумента КАК втДанныеДокумента
          |    ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
          |      &МоментВремени,
          |      Номенклатура В
          |        (ВЫБРАТЬ
          |          втДанныеДокумента.Номенклатура
          |        ИЗ
          |          втДанныеДокумента КАК втДанныеДокумента)) КАК ОстаткиНоменклатурыОстатки
          |      ПО втДанныеДокумента.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
          |
          |УПОРЯДОЧИТЬ ПО
          |  ОстаткиНоменклатурыОстатки.Партия.МоментВремени
          |ИТОГИ
          |  МИНИМУМ(НомерСтроки),
          |  МАКСИМУМ(Количество),
          |  МАКСИМУМ(Стоимость),
          |  МАКСИМУМ(ВидНоменклатуры),
          |  СУММА(КоличествоОстаток),
          |  СУММА(СтоимостьОстаток)
          |ПО
          |  Номенклатура";

        Запрос.УстановитьПараметр("МоментВремени", ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени()));
        Запрос.УстановитьПараметр("Ссылка", Ссылка);

        // Устанавливаем сортировку согласно текущему методу списания
        Если МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО Тогда
          Запрос.Текст = СтрЗаменить(
          Запрос.Текст, "
            |УПОРЯДОЧИТЬ ПО
            |    ОстаткиНоменклатурыОстатки.Партия.МоментВремени",
            "
            |УПОРЯДОЧИТЬ ПО
            |    ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ"
          );
        КонецЕсли;

        РезультатЗапроса = Запрос.Выполнить();
        ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

        // Выполняем обход выборки
        Пока ВыборкаНоменклатура.Следующий() Цикл
          // Если НЕ Услуга, то пытаемся сделать движения по регистру Остатки товаров
          Если Не ВыборкаНоменклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
            // Проверяем, хватает ли вообще товарононов. Если нет - сообщаем о нехватке и запрещаем проведение документа
            Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда 
              Сообщение = Новый СообщениеПользователю;
              Сообщение.Текст = "Не хватает номенклатуры " + ВыборкаНоменклатура.Номенклатура;;
              Сообщение.Поле = "Товары[" + Строка(ВыборкаНоменклатура.НомерСтроки - 1) + "].Количество";
              Сообщение.УстановитьДанные(ЭтотОбъект);
              Сообщение.Сообщить();
              Отказ = Истина;
              Продолжить;
            КонецЕсли;

            ОсталосьСписать = ВыборкаНоменклатура.Количество;
            Выборка = ВыборкаНоменклатура.Выбрать();
            Пока Выборка.Следующий() Цикл
              Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
              Если Списать = Выборка.КоличествоОстаток Тогда 
                Стоимость = Выборка.СтоимостьОстаток;
              Иначе
                Стоимость = Списать * Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток;
              КонецЕсли;

              Движение = Движения.ОстаткиНоменклатуры.Добавить();
              Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
              Движение.Период = Дата;
              Движение.Партия = Выборка.Партия;
              Движение.Номенклатура = Выборка.Номенклатура;
              Движение.Количество = Списать;
              Движение.Стоимость = Стоимость;

              ОсталосьСписать = ОсталосьСписать - Списать;
              Если ОсталосьСписать = 0 Тогда
                Прервать;
              КонецЕсли;
            КонецЦикла;
          КонецЕсли;
        КонецЦикла;
      Иначе
        // Отменяем проведение, если учетная политика не заполнена
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Учетная политика не задана!";
        Сообщение.Сообщить();
        Отказ = Истина;
        Возврат;
      КонецЕсли;

      Если Не Отказ Тогда
        Движения.ОстаткиНоменклатуры.Записывать = Истина;
      КонецЕсли;
    КонецПроцедуры
    ```

Сначала получаем текущую учетную политику и, если она не заполнена, отменяем проведение. Если учетная политика заполнена - делаем движения. Запоминем метод списания учетной политики.

    ```1C
    МетодСписания = УчетнаяПолитика.МетодСписания;
    ```

  Формируем запрос для выборки данных. Первым пакетом выбираем и помещаем во временную таблицу данные табличной части документа, такие как НомерСтроки - для вывода ошибки о нехватке товара, если такая будет, Номенклатура, Количество, Стоимость и ВидНоменклатуры, сгруппировав по полю Номенклатура. Второй - выбирает все данные из ранее созданной временной таблицы и левым соединением выбирает остатки из регистра Остатки номенклатуры в разрезе номенклатуры и партий товаров. Результат сортируется по партии и берутся итоги по номенклатуре:
    * МАКСИМУМ по номеру строки и полям ВидНоменклатуры, Количество и Стоимость, для того чтобы в верхней группировке знать, сколько товара и на какую сумму списать, а так же не делать расчета списания для услуг
    * СУММА КоличествоОстаток и СтоимостьОстаток, чтобы знать, сколько товара осталось для списания
  
  Устанавливаем параметры запроса. Ссылка - ссылка на документ, по которому делаются движения. МоментВремени - свойство МоментВремени документа. Оно однозначно указывает положение документа на временной шкале. Т.к. в одну секунду может быть проведено несколько документов, то поле Дата документа для нас не подходит. Причем берем не просто момент времени, а:

      ```1C
      ?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени()))
      ```

  `Неопределено` передаем для оптимизации запроса при оперативном проведении. Для регистров остатков система обеспечивает хранение рассчитанных итогов и их динамическое обновление при любых действиях с движениями (записи движений, отмены проведения документов, удалении документов). Итоги хранятся системой на текущий момент (точку актуальности) и на границы предыдущих периодов. В качестве периода может выступать месяц, пятнадцать дней, декада и пять дней. При получении актуальных остатков система будет выполнять запрос только к итогам регистра накопления, а для расчета итогов на определенный период будет выполняться запрос к итогам с объединением с основной таблицей регистра. `МоментВремени()` передаем для определения остатков на момент времени документа.
  
  Далее в зависимости от метода списания изменяем сортировку результата запроса по полю МоментВремени документа партии. Если ФИФО - оставляем запрос без изменения, а если ЛИФО, то заменяем в тексте запроса строку упорядочивания с “УПОРЯДОЧИТЬ ПО ОстаткиНоменклатурыОстатки.Партия.МоментВремени” на “УПОРЯДОЧИТЬ ПО ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ”. Обратите внимание, что ``сортировка происходит`` не по дате или ссылке, а именно ``по моменту времени партии``.
  
  Выполняем запрос и выборку из результата запроса с обходом по группировкам.
  Для проверки создадим приходные и расходные накладные

  > Приходная накладная 1:
  
        |   Номенклатура    |   Количество  |   Стоимость   |
        -----------------------------------------------------
        |   Торт Тирамису   |   2           |   1400        |
        |   Кекс            |   7           |   280         |

  > Приходная накладная 2

        |   Номенклатура    |   Количество  |   Стоимость   |
        -----------------------------------------------------
        |   Торт Тирамису   |   1           |   700         |

  > Приходная накладная 3:

        |   Номенклатура    |   Количество  |   Стоимость   |
        -----------------------------------------------------
        |   Кекс            |   3           |   120         |

  > Расходная накладная 1

        |   Номенклатура    |   Количество  |   Стоимость   |
        -----------------------------------------------------
        |   Доставка        |   1           |   100         |
        |   Торт Тирамису   |   1           |   1500        |

Итого до проведения расходной накладной у нас на остатке 3 торта “Тирамису”.

Поставим точку останова на строке, следующей за выполнением запроса. Выгрузка из результата запроса будет выглядеть так:

      |   Номенклатура        |   КоличествоОстаток   |  xxx  |
      ---------------------------------------------------------
      |   Доставка            |   1                   |  xxx  |
      |      Доставка         |   1                   |  xxx  |
      |   Торт Тирамису       |   3                   |  xxx  |
      |      Торт Тирамису    |   2                   |  xxx  |
      |      Торт Тирамису    |   1                   |  xxx  |

Правильно организуем циклы обхода выборки. Первый - по номенклатуре. Если это услуга, то движений по остаткам не делаем. Если Количество > КоличествоОстаток, значит номенклатуры не хватает. Выводим сообщение о нехватке и переходим к следующей группировке. Если нет - идем дальше.

Сохраняем в переменной ОсталосьСписать количество текущей номенклатуры, которое нужно списать, и переходим на следующий уровень выборки.

    ```1C
    Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
    Если Списать = Выборка.КоличествоОстаток Тогда 
      Стоимость = Выборка.СтоимостьОстаток;
    Иначе 
      Стоимость = Списать * Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток;
    КонецЕсли;
    ```

В переменную Списать выбираем минимальное значение из ОсталосьСписать и остатка в текущей партии. Если результат этой операции не равен КоличествоОстаток, тогда сумму списания раcчитываем как Количество для списания умноженное на СтоимостьОстаток и разделенное на КоличествоОстаток. Понятно, по какому принципу считается стоимость списания во втором случае - рассчитываем стоимость единицы товара и умножаем на количество, которое нужно списать. Для чего нужно приравнивать стоимость списания к оставшейся стоимости? Расчеты с дробными числами неточные, поэтому могут оставаться “копейки”. Т.е. мы можем списать все количество по партии, а стоимость в “0” может не сойтись.
  
Добавляем движение по остаткам.
  
В конце цикла уменьшаем Значение ОсталосьСписать на стоимость списания и, если ОсталосьСписать равно 0, прерываем выполнение цикла.
  
В конце процедуры проведения, если не было ошибок, устанавливаем для регистра остатков свойство Записывать как Истина.
  
Проведем документ реализации и проверим его движения.
  
  > Движения - Остатки номенклатуры - Расходная накладная 1

        |   Номенклатура    |   Партия                  |   Количество   |   Стоимость   |
        ----------------------------------------------------------------------------------
        |   Торт Тирамису   |   Приходная накладная 1   |   1            |   700         |

Проверим результат выполнения запроса при проведении.

      |   Номенклатура        |   КоличествоОстаток   |  xxx  |
      ---------------------------------------------------------
      |   Доставка            |   1                   |  xxx  |
      |      Доставка         |   1                   |  xxx  |
      |   Торт Тирамису       |   3                   |  xxx  |
      |      Торт Тирамису    |   2                   |  xxx  |
      |      Торт Тирамису    |   1                   |  xxx  |

  Остатки такие, какими должны быть. Что будет, если мы изменим дату документа? Если уменьшим, то остатки останутся такими-же. Но если увеличим, то вот что получим после выполнения запроса.
  
      |   Номенклатура        |   КоличествоОстаток   |  xxx  |
      ---------------------------------------------------------
      |   Доставка            |   1                   |  xxx  |
      |      Доставка         |   1                   |  xxx  |
      |   Торт Тирамису       |   2                   |  xxx  |
      |      Торт Тирамису    |   1                   |  xxx  |
      |      Торт Тирамису    |   1                   |  xxx  |

Остатки изменились. Это произошло потому, что остались “старые” движения документа. При открытии документ считал их из БД. Чтобы избежать этого перед выполнением запроса необходимо очистить предыдущие движения. Для этого напишем такой код, одновременно очистив движения по регистру Продажи.

    ```1C
    Движения.ОстаткиНоменклатуры.Очистить();
    Движения.ОстаткиНоменклатуры.Записать();

    Движения.Продажи.Очистить();
    ```

Свойство регистра накопления `БлокироватьДляИзменения` позволит заблокировать от чтения те данные, которые будут удалены из регистра, чтобы никто не прочитал их. Записать - запишет пустые движения и выполнит описанную выше блокировку.

Попробуем снова увеличить дату документа и провести его. Проверим, что выдаст результат запроса.

      |   Номенклатура        |   КоличествоОстаток   |  xxx  |
      ---------------------------------------------------------
      |   Доставка            |   1                   |  xxx  |
      |      Доставка         |   1                   |  xxx  |
      |   Торт Тирамису       |   3                   |  xxx  |
      |      Торт Тирамису    |   2                   |  xxx  |
      |      Торт Тирамису    |   1                   |  xxx  |

Остатки выглядят так, как и должны.

На первый взгляд все правильно, но какая ситуация сейчас может возникнуть. Мы выполняем проведение документа. В обработке проведения выполняется запрос и мы начинаем обход результата запроса. В это время может быть проведен другой документ, который изменит остатки по нашей номенклатуре, что может привести к отрицательным остаткам. Проведем эксперимент Создадим 2 сеанса в режиме Предприятие. Первый - отладочный, а второй - нет. Наш тестовый документ проведем в отладочном, предварительно поставив точку останова на строке после выполнения запроса. Во втором предприятии создадим расходную накладную и в табличную часть добавим “Торт Тирамису” с количеством 3 (столько у нас на остатке без учета движений первой расходной накладной) и проведем ее. Получаем, что первый документ выполнил выборку данных но еще не выполнил движения и на остатке для него сейчас 3 торта, а второй считал остатки, так же узнал, что на остатке 3 торта (т.к. первый документ движений еще не сделал). Итог всего этого - на остатке было 3 торта, первый документ спишет 1, второй 3 и останется -1 (отрицательный остаток).

Что бы этого не произошло перед выполнением запроса необходимо заблокировать регистр. Точнее - заблокировать записи по товарам из нашего документа. Для этого воспользуемся объектом БлокировкаДанных.

    ```1C
    Блокировки = Новый БлокировкаДанных;
    Блокировка = Блокировки.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
    Блокировка.Режим = РежимБлокировкиДанных.Исключительный;
    Блокировка.ИсточникДанных = СписокНоменклатуры;
    Блокировка.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
    Блокировки.Заблокировать();

    РезультатЗапроса = Запрос.Выполнить();
    ```

Строка `Блокировки = Новый БлокировкаДанных;` создает объект БлокировкаДанных. Далее добавляем к нему блокировку по регистру Остатки номенклатуры с исключительным режимом блокировки. Данные, по которым будем блокировать регистр, берем из табличной части Товары документа и блокировать будем по полю Номенклатура. После проведенных манипуляций увидим, что после того, как первый документ остановится в отладке после выполнения запроса (блокировки выполняются перед его выполнением) второй документ не будет проведен пока первый не “отпустит” блокировку.
  
Осталось сделать движения по регистру Продажи и отчеты.

    ```1C
    // Выполняем обход выборки
    Пока ВыборкаНоменклатура.Следующий() Цикл
      Себестоимость = 0;
      // Если НЕ Услуга, то пытаемся сделать движения по регистру Остатки товаров
      Если Не ВыборкаНоменклатура.Услуга Тогда
        // Проверяем, хватает ли вообще товаров. Если нет - сообщаем о нехватке и запрещаем проведение документа
        Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда 
          Сообщение = Новый СообщениеПользователю;
          Сообщение.Текст = "Не хватает номенклатуры " + ВыборкаНоменклатура.Номенклатура;;
          Сообщение.Поле = "Товары[" + Строка(ВыборкаНоменклатура.НомерСтроки - 1) + "].Количество";
          Сообщение.УстановитьДанные(ЭтотОбъект);
          Сообщение.Сообщить();
          Отказ = Истина;
          Продолжить;
        КонецЕсли;

        ОсталосьСписать = ВыборкаНоменклатура.Количество;
        Выборка = ВыборкаНоменклатура.Выбрать();
        Пока Выборка.Следующий() Цикл
          Списать = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
          Если Списать = Выборка.КоличествоОстаток Тогда
            Стоимость = Выборка.СтоимостьОстаток;
          Иначе
            Стоимость = Списать * Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток;
          КонецЕсли;

          Движение = Движения.ОстаткиТоваров.Добавить();
          Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
          Движение.Период = Дата;
          Движение.Партия = Выборка.Партия;
          Движение.Номенклатура = Выборка.Номенклатура;
          Движение.Количество = Списать;
          Движение.Стоимость = Стоимость;

          ОсталосьСписать = ОсталосьСписать - Списать;
          Себестоимость = Себестоимость + Стоимость;
          Если ОсталосьСписать = 0 Тогда
            Прервать;
          КонецЕсли;
        КонецЦикла;
      КонецЕсли;

      Движение = Движения.Продажи.Добавить();
      Движение.Период = Дата;
      Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
      Движение.Количество = ВыборкаНоменклатура.Количество;
      Движение.Стоимость = ВыборкаНоменклатура.Стоимость;
      Движение.Себестоимость = Себестоимость;
    КонецЦикла;
    ```

И не забываем разрешить запись движений по продажам

    ```1C
    Если Не Отказ Тогда
      Движения.ОстаткиТоваров.Записывать = Истина;
      Движения.Продажи.Записывать = Истина;
    КонецЕсли;
    ```