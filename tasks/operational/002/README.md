# Тезисы

* Торговая компания покупает и продает товары/услуги. Товары и услуги указываются в одной табличной части
* Учет товаров по складам не ведется
* Контроль остатков с оповещением о нехватке товаров при продаже
* Списание себестоимости товаров должно быть организовано по партиям и зависеть от текущей учетной политики
* В первую очередь товар должен списываться из указанной в шапке партии и только потом в соответствии с текущей учетной политикой
* Учетная политика устанавливается год. На следующий год может измениться
* Структуры требуемых отчетов
  * Продажи: Номенклатура, Количество, Себестоимость, Стоимость, Прибыль, где Прибыль считается как "Стоимость - Себестоимость"
  
        |   Номенклатура    |   Количество  |   Себестоимость   |   Стоимость   |   Прибыль |
        -------------------------------------------------------------------------------------
        |   Торт Тирамису   |   2           |   1400            |   2600        |   1200    |
        |   Кекс            |   6           |   240             |   600         |   360     |
        |   Доставка        |   1           |                   |   100         |   100     |

  * Остатки: Номенклатура, Партия, Количество, Стоимость с группировкой по полю Номенклатура
  
        |   Номенклатура    |   Партия                  |   Количество  |   Стоимость   |
        ---------------------------------------------------------------------------------
        |   Торт Тирамису   |                           |   3           |   2100        |
        |                   |   Приходная накладная 1   |   2           |   1400        |
        |                   |   Приходная накладная 2   |   1           |   700         |
        |   Кекс            |                           |   10          |   400         |
        |                   |   Приходная накладная 1   |   7           |   280         |
        |                   |   Приходная накладная 3   |   3           |   120         |

## Решение

### Исследование тезисов

* ***"Торговая компания покупает и продает товары/услуги. Товары и услуги указываются в одной табличной части".*** Для операций покупки/продажи потребуются 2 документа - "Приходная накладная" и "Расходная накладная". Среди товаров будут услуги, значит, требуется справочник "Товары" с признаком, услуга это или нет.
* ***"Учет товаров по складам не ведется".*** Значит, весть товар организации хранится на одном складе и хранить остатки в разрезе складов не требуется.
* ***"Контроль остатков с оповещением о нехватке товаров при продаже".*** При проведении документа "Расходная накладная" необходимо учитывать остатки товаров и, если товара не хватает, запрещать проводить документ, сообщать о нехватке товара.
* ***"Списание себестоимости товаров должно быть организовано по партиям и зависеть от текущей учетной политики. Учетная политика устанавливается год. На следующий год может измениться".*** Нужно вести учет в разрезе партий. Т.к. значения учетной политики изменяться не будут, для их хранения используем объект Перечисление. Списание стоимости нужно организовать по методу, установленному учетной политикой на текущий год. Для учетной политики будем использовать регистр сведений с периодичностью "Год".
* ***"В первую очередь товар должен списываться из указанной в шапке партии и только потом в соответствии с текущей учетной политикой".*** Необходимо иметь возможность указать партию, из которой в первую очередь списывать товары.

Исходя из структуры требуемых отчетов видно, что нужно знать остатки товаров и продажи. В обоих случаях будем использовать регистры накопления. Для учета остатков - регистр накопления Остатки товаров с полями Номенклатура, Партия, Количество, Стоимость, где партия - документ "Приходная накладная". Для учета продаж буем использовать оборотный регистр накопления с полями Номенклатура, Количество, Стоимость, Себестоимость. Прибыль записывать не будем, так как ее можно рассчитать как "Стоимость - Себестоимость".

### Разработка

#### Начнем разработку с изменения структуры данных

Структура измененных объектов:

* Справочники
  * Номенклатура
    * Реквизиты
      * ВидНоменклатуры (ПеречислениеСсылка.ВидыНоменклатуры)
* Документы
  * РасходнаяНакладная
    * Реквизиты
      * Партия
    * Табличные части
      * СписокНоменклатуры
        * Номенклатура
        * Количество
        * Сумма
* Регистры сведений
  * УчетнаяПолитика
    * Периодичность - В пределах года
    * Ресурсы
      * УчетнаяПолитика (ПеречислениеСсылка.УчетнаяПолитика)
* Регистры накопления
  * ОстаткиНоменклатуры
    * Вид регистра - Остатки
    * Измерения
      * Номенклатура (СправочникСсылка.Номенклатура)
      * Партия (ДокументСсылка.ПриходнаяНакладная)
    * Ресурсы
      * Количество (Число, 10.0)
      * Стоимость (Число, 12.2)
  * Продажи
    * Вид регистра - Обороты
    * Измерения
      * Номенклатура (СправочникСсылка.Номенклатура)
    * Ресурсы
      * Количество (Число, 10.0)
      * Стоимость (Число, 12.2)
      * Себестоимость (Число, 12.2)

#### Движения документов

Обработка проведения приходной накладной не изменяется.

Для упорядочивания партий по выбранной в шапке необходимо доработать запрос и изменение условий сортировки.

* В втДанныеДокумента выбираем партию из шапки документа

* Сравниваем партии документа и остатков: **НЕ втДанныеДокумента.ПартияДокумента = ОстаткиНоменклатурыОстатки.Партия**. Результатом сравнения является значение типа БУЛЕВО, значит, чтобы значения с совпадающими партиями обрабатывались первыми, нужно инвертировать значение конструкцией **НЕ** (ЛОЖЬ меньше чем ИСТИНА)

* В код с изменением текста запроса добавляем ПорядокПартии.

    ```1C
    Запрос.Текст =
      "ВЫБРАТЬ
      |  МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
      |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
      |  СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
      |  СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
      |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
      |  РасходнаяНакладнаяСписокНоменклатуры.Ссылка.Партия КАК ПартияДокумента
      |ПОМЕСТИТЬ втДанныеДокумента
      |ИЗ
      |  Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
      |ГДЕ
      |  РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
      |
      |СГРУППИРОВАТЬ ПО
      |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
      |  РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры,
      |  РасходнаяНакладнаяСписокНоменклатуры.Ссылка.Партия
      |
      |ИНДЕКСИРОВАТЬ ПО
      |  Номенклатура
      |;
      |
      |////////////////////////////////////////////////////////////////////////////////
      |ВЫБРАТЬ
      |  втДанныеДокумента.НомерСтроки КАК НомерСтроки,
      |  втДанныеДокумента.Номенклатура КАК Номенклатура,
      |  втДанныеДокумента.Количество КАК Количество,
      |  втДанныеДокумента.Стоимость КАК Стоимость,
      |  втДанныеДокумента.ВидНоменклатуры КАК ВидНоменклатуры,
      |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
      |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
      |  ЕСТЬNULL(ОстаткиНоменклатурыОстатки.Партия, ЗНАЧЕНИЕ(Документ.ПриходнаяНакладная.)) КАК Партия,
      |  НЕ втДанныеДокумента.ПартияДокумента = ОстаткиНоменклатурыОстатки.Партия КАК ПорядокПартии
      |ИЗ
      |  втДанныеДокумента КАК втДанныеДокумента
      |    ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
      |      &МоментВремени,
      |      Номенклатура В
      |        (ВЫБРАТЬ
      |          втДанныеДокумента.Номенклатура
      |        ИЗ
      |          втДанныеДокумента КАК втДанныеДокумента)) КАК ОстаткиНоменклатурыОстатки
      |      ПО втДанныеДокумента.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
      |
      |УПОРЯДОЧИТЬ ПО
      |  ПорядокПартии,
      |  ОстаткиНоменклатурыОстатки.Партия.МоментВремени
      |ИТОГИ
      |  МИНИМУМ(НомерСтроки),
      |  МАКСИМУМ(Количество),
      |  МАКСИМУМ(Стоимость),
      |  МАКСИМУМ(ВидНоменклатуры),
      |  СУММА(КоличествоОстаток),
      |  СУММА(СтоимостьОстаток)
      |ПО
      |  Номенклатура";

    Запрос.УстановитьПараметр("МоментВремени", ?(РежимПроведения = РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени()));
    Запрос.УстановитьПараметр("Ссылка", Ссылка);

    // Устанавливаем сортировку согласно текущему методу списания
    Если МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО Тогда
      Запрос.Текст = СтрЗаменить(
        Запрос.Текст,
        "
        |УПОРЯДОЧИТЬ ПО
        |  ПорядокПартии,
        |  ОстаткиНоменклатурыОстатки.Партия.МоментВремени",
        "
        |УПОРЯДОЧИТЬ ПО
        |  ПорядокПартии,
        |  ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ"
      );
    КонецЕсли;
    ```
