# Тезисы

* Взаиморасчеты с контргентами ведутся в разрезе договоров
* С контрагентом можно заключить неограниченное количество договоров
* Долг контрагента возникает при проведении расходной накладной. Контрагент и его договор указываются в шапке расходной накладной. Запрещено указывать не соответствующий контрагенту договор
* Проводки расходной накладной:

    ```Text
    Дт "Покупатели" - Кт "Прибыли и убытки" на сумму продажи
    ```

* Погашение задолженности контрагента осуществляется документом "Приход денег", в табличной части которого указываются `Контрагент`, `Договор контрагента` и `Сумма`
* Задолженность может гаситься частями
* Документ прихода денег не должен проводиться, если сумма оплаты больше суммы задолженности по договору
* Проводки прихода денег:

    ```Text
    Дт "Касса" - Кт "Покупатели" на указанную сумму
    ```

* Структуры требуемых отчетов
  * Задолженность покупателей

        |   Контрагент    |   Договор         |   Сумма   |
        ---------------------------------------------------
        |   Тортоед       |                   |   3       |
        |   Тортоед       |   Основной        |   1       |
        |   Тортоед       |   Дополнительный  |   2       |

## Решение

### Исследование тезисов

* ***"Взаиморасчеты с контргентами ведутся в разрезе договоров. С контрагентом можно заключить неограниченное количество договоров. Запрещено указывать не соответствующий контрагенту договор".*** Потребуются справочники Контрагенты и ДоговорыКонтрагентов. Справочник ДоговорыКонтрагентов подчинен справочнику Контрагенты.
* ***"Долг контрагента возникает при проведении расходной накладной. Контрагент и его договор указываются в шапке расходной накладной".*** В шапке расходной накладной необходимо заполнять контрагента и его договор.
* ***"Погашение задолженности контрагента осуществляется документом "Приход денег", в табличной части которого указываются `Контрагент`, `Договор контрагента` и `Сумма`".*** Нужен документ `ПриходДенег` с табличной частью с требуемыми реквизитами.
* ***"Задолженность может гаситься частями. Документ прихода денег не должен проводиться, если сумма оплаты больше суммы задолженности по договору".*** При проведении документа прихода денег необходимо контролировать остаток задолженности контрагента по договору.

### Разработка

#### Начнем разработку с изменения структуры данных

Решать задачу будем на базе решения задачи 001 оперативного учета.

Структура измененных объектов:

* Справочники
  * ДоговорыКонтрагентов
    * Владельцы
      * Справочник.Контрагенты
* Документы
  * РасходнаяНакладная
    * Реквизиты
      * Контрагент (СправочникСсылка.Контрагенты)
      * ДоговорКонтрагента (СправочникСсылка.ДоговорыКонтрагентов). Для свойства на форме элемента необходимо установить связи параметров выбора по контрагенту владельцу
  * ПриходДенег
    * ТабличныеЧасти
      * Состав
        * Контрагент (СправочникСсылка.Контрагенты)
        * ДоговорКонтрагента (СправочникСсылка.ДоговорыКонтрагентов). Для свойства на форме элемента необходимо установить связи параметров выбора по контрагенту владельцу
        * Сумма (Число)
* Планы счетов
  * Управленческий
    * Максимальное количество субконто - 2
    * Виды субконто - (ПланыВидовХарактеристик.ВидыСубконто)
    * Предопределенные
      * Активы
        * Покупатели
          * Виды субконто
            * Контрагент
            * ДоговорКонтрагента
* Планы видов характеристик
  * ВидыСубконто
    * Тип значения характеристик
      * (СправочникСсылка.Субконто)
      * (СправочникСсылка.Контрагенты)
      * (СправочникСсылка.ДоговорыКонтрагентов)
    * Дополнительные значения характеристик - (СправочникСсылка.Субконто)
    * Предопределенные
      * Контрагент (СправочникСсылка.Контрагенты)
      * ДоговорКонтрагента (СправочникСсылка.ДоговорыКонтрагентов)
* Регистры бухгалтерии
  * Управленческий
    * ПланСчетов - Управленческий. Уже есть в каркасной конфигурации
    * Корреспонденция - Истина
    * Ресурсы
      * Сумма (Число)

Так как отчет должен строиться по задолженности контрагентов в разрезе договоров, то для плана счетов устанавливается максимальное количество субконто равным 2 и к счету `Покупатели` добавляем 2 вида субконто: Контрагент и ДоговорКонтрагента

### Проведение документа Расходная накладная

При проведении расходной накладной по регистру `Управленческий` контроль остатков не требуется, значит не требуется и делать какие-либо блокировки по нему. Для формирования движений воспользуемся конструктором движений. Изменения о обработке проведения:

    ```1С
    Если Не Отказ Тогда
      Движения.ОстаткиНоменклатуры.Записывать = Истина;
      Движения.Продажи.Записывать = Истина;

      // регистр Управленческий
      Движения.Управленческий.Записывать = Истина;
      Движение = Движения.Управленческий.Добавить();
      Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
      Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
      Движение.Период = Дата;
      Движение.Сумма = СуммаПоДокументу;
      Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент] = Контрагент;
      Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДоговорКонтрагента] = ДоговорКонтрагента;
    КонецЕсли;
    ```

### Проведение документа Приход денег

Шаги обработки проведения:

* Выполняем группировку данных табличной части документа. Дополнительно помещаем данные во временную таблицу (пригодятся при контроле остатков).

    ```1C
    Запрос = Новый Запрос();
    Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос.Текст =
      "ВЫБРАТЬ
      |  ПриходДенегСостав.Контрагент,
      |  ПриходДенегСостав.ДоговорКонтрагента,
      |  СУММА(ПриходДенегСостав.Сумма) КАК Сумма
      |ПОМЕСТИТЬ втДанныеДокумента
      |ИЗ
      |  Документ.ПриходДенег.Состав КАК ПриходДенегСостав
      |ГДЕ
      |  ПриходДенегСостав.Ссылка = &Ссылка
      |
      |СГРУППИРОВАТЬ ПО
      |  ПриходДенегСостав.Контрагент,
      |  ПриходДенегСостав.ДоговорКонтрагента
      |;
      |
      |////////////////////////////////////////////////////////////////////////////////
      |ВЫБРАТЬ
      |  втДанныеДокумента.Контрагент,
      |  втДанныеДокумента.ДоговорКонтрагента,
      |  втДанныеДокумента.Сумма
      |ИЗ
      |  втДанныеДокумента КАК втДанныеДокумента";
    ```

* Заполняем движения и записываем их. Свойстро `БлокироватьДляИзменения` регистра `Управленческий` устанавливаем в **Истина**. После `Движения.Записать();` данные регистра будут заблокированы и можно будет производить контроль костатков.

    ```1C
    Движения.Управленческий.Записывать = Истина;
    Движения.Управленческий.БлокироватьДляИзменения = Истина;
    Движения.Управленческий.Очистить();

    ДанныеДокумента = Запрос.Выполнить().Выгрузить();

    ПланыСчетовКасса = ПланыСчетов.Управленческий.Касса;
    ПланыСчетовПокупатели = ПланыСчетов.Управленческий.Покупатели;
    ВидыСубконтоКонтрагент = ПланыВидовХарактеристик.ВидыСубконто.Контрагент;
    ВидыСубконтоДоговорКонтрагента = ПланыВидовХарактеристик.ВидыСубконто.ДоговорКонтрагента;

    Для Каждого СтрДанныеДокумента Из ДанныеДокумента Цикл
      Движение = Движения.Управленческий.Добавить();
      Движение.СчетДт = ПланыСчетовКасса;
      Движение.СчетКт = ПланыСчетовПокупатели;
      Движение.Период = Дата;
      Движение.Сумма = СтрДанныеДокумента.Сумма;
      Движение.СубконтоКт[ВидыСубконтоКонтрагент] = СтрДанныеДокумента.Контрагент;
      Движение.СубконтоКт[ВидыСубконтоДоговорКонтрагента] = СтрДанныеДокумента.ДоговорКонтрагента;
    КонецЦикла;

    Движения.Записать();
    ```

* Выполняем контроль остатков (минусы по остаткам сумм договоров контрагентов). Остатки контролируем по счету `Управленческий`. При неоперативном режиме проведения в качестве периода передаем границу `Включая` момент времени текущего документа, чтобы его проводки попали в выборку.

    ```1C
    Запрос.Текст =
      "ВЫБРАТЬ
      |  ПРЕДСТАВЛЕНИЕ(УправленческийОстатки.Субконто1) КАК КонтрагентПредставление,
      |  ПРЕДСТАВЛЕНИЕ(УправленческийОстатки.Субконто2) КАК ДоговорПредставление,
      |  -УправленческийОстатки.СуммаОстаток КАК Остаток
      |ИЗ
      |  РегистрБухгалтерии.Управленческий.Остатки(
      |      &МоментВремени,
      |      Счет = &Счет,
      |      ,
      |      (Субконто1, Субконто2) В
      |        (ВЫБРАТЬ
      |           втДанныеДокумента.Контрагент,
      |           втДанныеДокумента.ДоговорКонтрагента
      |         ИЗ
      |           втДанныеДокумента КАК втДанныеДокумента)) КАК УправленческийОстатки
      |ГДЕ
      |  УправленческийОстатки.СуммаОстаток < 0";

    Запрос.УстановитьПараметр("Счет", ПланыСчетовПокупатели);
    Запрос.УстановитьПараметр(
      "МоментВремени",
      ?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая))
    );

    РезультатЗапроса = Запрос.Выполнить();
    Если Не РезультатЗапроса.Пустой() Тогда
      Отказ = Истина;

      Выборка = РезультатЗапроса.Выбрать();
      Пока Выборка.Следующий() Цикл
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Сумма оплаты по договору " + Выборка.ДоговорПредставление +
          " контрагента " + Выборка.КонтрагентПредставление +
          " превышает сумму задолженности на " + Строка(Выборка.Остаток);
        Сообщение.Сообщить();
      КонецЦикла;
    КонецЕсли;
    ```

Код получившегося модуля:

    ```1C
    Запрос = Новый Запрос();
    Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос.Текст =
      "ВЫБРАТЬ
      |  ПриходДенегСостав.Контрагент,
      |  ПриходДенегСостав.ДоговорКонтрагента,
      |  СУММА(ПриходДенегСостав.Сумма) КАК Сумма
      |ПОМЕСТИТЬ втДанныеДокумента
      |ИЗ
      |  Документ.ПриходДенег.Состав КАК ПриходДенегСостав
      |ГДЕ
      |  ПриходДенегСостав.Ссылка = &Ссылка
      |
      |СГРУППИРОВАТЬ ПО
      |  ПриходДенегСостав.Контрагент,
      |  ПриходДенегСостав.ДоговорКонтрагента
      |;
      |
      |////////////////////////////////////////////////////////////////////////////////
      |ВЫБРАТЬ
      |  втДанныеДокумента.Контрагент,
      |  втДанныеДокумента.ДоговорКонтрагента,
      |  втДанныеДокумента.Сумма
      |ИЗ
      |  втДанныеДокумента КАК втДанныеДокумента";

    Запрос.УстановитьПараметр("Ссылка", Ссылка);

    // регистр Управленческий
    Движения.Управленческий.Записывать = Истина;
    Движения.Управленческий.БлокироватьДляИзменения = Истина;
    Движения.Управленческий.Очистить();

    ДанныеДокумента = Запрос.Выполнить().Выгрузить();

    ПланыСчетовКасса = ПланыСчетов.Управленческий.Касса;
    ПланыСчетовПокупатели = ПланыСчетов.Управленческий.Покупатели;
    ВидыСубконтоКонтрагент = ПланыВидовХарактеристик.ВидыСубконто.Контрагент;
    ВидыСубконтоДоговорКонтрагента = ПланыВидовХарактеристик.ВидыСубконто.ДоговорКонтрагента;

    Для Каждого СтрДанныеДокумента Из ДанныеДокумента Цикл
      Движение = Движения.Управленческий.Добавить();
      Движение.СчетДт = ПланыСчетовКасса;
      Движение.СчетКт = ПланыСчетовПокупатели;
      Движение.Период = Дата;
      Движение.Сумма = СтрДанныеДокумента.Сумма;
      Движение.СубконтоКт[ВидыСубконтоКонтрагент] = СтрДанныеДокумента.Контрагент;
      Движение.СубконтоКт[ВидыСубконтоДоговорКонтрагента] = СтрДанныеДокумента.ДоговорКонтрагента;
    КонецЦикла;

    Движения.Записать();

    Запрос.Текст =
      "ВЫБРАТЬ
      |  ПРЕДСТАВЛЕНИЕ(УправленческийОстатки.Субконто1) КАК КонтрагентПредставление,
      |  ПРЕДСТАВЛЕНИЕ(УправленческийОстатки.Субконто2) КАК ДоговорПредставление,
      |  -УправленческийОстатки.СуммаОстаток КАК Остаток
      |ИЗ
      |  РегистрБухгалтерии.Управленческий.Остатки(
      |      &МоментВремени,
      |      Счет = &Счет,
      |      ,
      |      (Субконто1, Субконто2) В
      |        (ВЫБРАТЬ
      |           втДанныеДокумента.Контрагент,
      |           втДанныеДокумента.ДоговорКонтрагента
      |         ИЗ
      |           втДанныеДокумента КАК втДанныеДокумента)) КАК УправленческийОстатки
      |ГДЕ
      |  УправленческийОстатки.СуммаОстаток < 0";

    Запрос.УстановитьПараметр("Счет", ПланыСчетовПокупатели);
    Запрос.УстановитьПараметр(
      "МоментВремени",
      ?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(), ВидГраницы.Включая))
    );

    РезультатЗапроса = Запрос.Выполнить();
    Если Не РезультатЗапроса.Пустой() Тогда
      Отказ = Истина;

      Выборка = РезультатЗапроса.Выбрать();
      Пока Выборка.Следующий() Цикл
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Сумма оплаты по договору " + Выборка.ДоговорПредставление +
          " контрагента " + Выборка.КонтрагентПредставление +
          " превышает сумму задолженности на " + Строка(Выборка.Остаток);
        Сообщение.Сообщить();
      КонецЦикла;
    КонецЕсли;
    ```